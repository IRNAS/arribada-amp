// undervoltage protection, turn things off and sleep
On battery#voltage do
  if [battery#voltage]<3200
      gpio,15,0 //turn all off
      gpio,12,0
      gpio,14,0
      TaskValueSet,3,1,0 // go to sleep procedure
  endif
endon

On System#Boot do //on boot
  timerSet,2,120  // start router popwer up cycle
endon

// check if brigt enough to take photos
On ldr#lux do
  if [ldr#lux]>40 // turn things on
    gpio,15,1
    gpio,12,1
    TaskValueSet,3,1,1 // let RPi know it should be on
  endif
  if [ldr#lux]<10
    TaskValueSet,3,1,0 // let RPi know it should turn off
    timerSet,1,45     // go to sleep 45s after sleep condition is met, delay must be less then ldr events
  endif
endon

On Rules#Timer=1 do  // going to sleep
 deepSleep,600 // go to sleep for 600s
endon

On Rules#Timer=2 do  // turn router on for 120s if RPi is on
 gpio,14,[var#rpi] // router power
 TaskValueSet,3,3,[var#rpi]
 timerSet,3,120 // keep on for 3min
endon

On Rules#Timer=3 do  // turn router off for 720s
 gpio,14,0 // router power
 TaskValueSet,3,3,0
 timerSet,2,720 // keep off for 12min
endon

On System#Sleep do //on sleep
 gpio,15,0
 gpio,12,0
 gpio,14,0
endon
